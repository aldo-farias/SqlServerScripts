1º passo: Cria tabela que ira armazenar os resultados do trace (Opcional)

use[master]

go

CREATE TABLE dbo.Traces(
    TextData VARCHAR(MAX) NULL,
    NTUserName VARCHAR(128) NULL,
    HostName VARCHAR(128) NULL,
    ApplicationName VARCHAR(128) NULL,
    LoginName VARCHAR(128) NULL,
    SPID INT NULL,
    Duration NUMERIC(15, 2) NULL,
    StartTime DATETIME NULL,
    EndTime DATETIME NULL,
    Reads INT,
    Writes INT,
    CPU INT,
    ServerName VARCHAR(128) NULL,
    DataBaseName VARCHAR(128),
    RowCounts INT,
    SessionLoginName VARCHAR(128))
-- Para realizar as querys de busca pela data que a query rodou.    
CREATE CLUSTERED INDEX SK01_Traces on Traces(StartTime) with(FILLFACTOR=95)

------------------------------

2º Passo: Cria procedure que configura o trace(colunas,filtros etc..) e especifica o caminho fisico que o mesmo ficara no S.O.

use [master]

go

CREATE PROCEDURE [dbo].[stpCreate_Trace]
AS
BEGIN
    declare @rc int, @TraceID int, @maxfilesize bigint, @on bit, @intfilter int, @bigintfilter bigint
    select @on = 1, @maxfilesize = 50
    -- Criação do trace
    exec @rc = sp_trace_create @TraceID output, 0, N'E:\Trace\Querys_Demoradas\Querys_Demoradas', @maxfilesize, NULL
    if (@rc != 0) goto error
    exec sp_trace_setevent @TraceID, 10, 1, @on
    exec sp_trace_setevent @TraceID, 10, 6, @on
    exec sp_trace_setevent @TraceID, 10, 8, @on
    exec sp_trace_setevent @TraceID, 10, 10, @on
    exec sp_trace_setevent @TraceID, 10, 11, @on
    exec sp_trace_setevent @TraceID, 10, 12, @on
    exec sp_trace_setevent @TraceID, 10, 13, @on
    exec sp_trace_setevent @TraceID, 10, 14, @on
    exec sp_trace_setevent @TraceID, 10, 15, @on
    exec sp_trace_setevent @TraceID, 10, 16, @on
    exec sp_trace_setevent @TraceID, 10, 17, @on
    exec sp_trace_setevent @TraceID, 10, 18, @on
    exec sp_trace_setevent @TraceID, 10, 26, @on
    exec sp_trace_setevent @TraceID, 10, 35, @on
    exec sp_trace_setevent @TraceID, 10, 40, @on
    exec sp_trace_setevent @TraceID, 10, 48, @on
    exec sp_trace_setevent @TraceID, 10, 64, @on
    exec sp_trace_setevent @TraceID, 12, 1,  @on
    exec sp_trace_setevent @TraceID, 12, 6,  @on
    exec sp_trace_setevent @TraceID, 12, 8,  @on
    exec sp_trace_setevent @TraceID, 12, 10, @on
    exec sp_trace_setevent @TraceID, 12, 11, @on
    exec sp_trace_setevent @TraceID, 12, 12, @on
    exec sp_trace_setevent @TraceID, 12, 13, @on
    exec sp_trace_setevent @TraceID, 12, 14, @on
    exec sp_trace_setevent @TraceID, 12, 15, @on
    exec sp_trace_setevent @TraceID, 12, 16, @on
    exec sp_trace_setevent @TraceID, 12, 17, @on
    exec sp_trace_setevent @TraceID, 12, 18, @on
    exec sp_trace_setevent @TraceID, 12, 26, @on
    exec sp_trace_setevent @TraceID, 12, 35, @on
    exec sp_trace_setevent @TraceID, 12, 40, @on
    exec sp_trace_setevent @TraceID, 12, 48, @on
    exec sp_trace_setevent @TraceID, 12, 64, @on
    set @bigintfilter = 80000000 -- Queries que demoram 1.3 Minutos ou mais
    exec sp_trace_setfilter @TraceID, 13, 0, 4, @bigintfilter
    -- Set the trace status to start
    exec sp_trace_setstatus @TraceID, 1
    goto finish
    error:
    select ErrorCode=@rc
    finish:
END

--------------------

3º Passo: Cria o trace no banco e no caminho "E:\Trace\Querys_Demoradas\Querys_Demoradas.trn"

exec dbo.stpCreate_Trace

---------------------

4º Passo: Verifica se o trace foi criado.

SELECT *
FROM :: fn_trace_getinfo(default)
where value = 'E:\Trace\Querys_Demoradas\Querys_Demoradas.trc'

-------------------------

5º Passo: Verifica as queries que o trace pegou.

Select Textdata, NTUserName, HostName, ApplicationName, LoginName, SPID, cast(Duration /1000/1000.00 as numeric(15,2)) Duration, Starttime,
    EndTime, Reads,writes, CPU, Servername, DatabaseName, rowcounts, SessionLoginName
FROM :: fn_trace_gettable('E:\Trace\Querys_Demoradas\Querys_Demoradas.trc', default)
where Duration is not null

-------------------------

6º Passo: Delatar o trace do banco e fechar a conexão com o servidor. Após esse processo sera necessario excluir manualmente o trace no local do arquivo (E:\Trace\Querys_Demoradas\Querys_Demoradas.trc)

Declare @Trace_Id int
SELECT @Trace_Id = TraceId
FROM fn_trace_getinfo(0)
where value = 'E:\Trace\Querys_Demoradas\Querys_Demoradas.trc'
exec sp_trace_setstatus  @traceid = @Trace_Id,  @status = 0 -- Interrompe o rastreamento especificado.
exec sp_trace_setstatus  @traceid = @Trace_Id,  @status = 2 --Fecha o rastreamento especificado e exclui sua definição do servidor.


----------------------------------------------------------------//---------------------------------------------------------------------
 
7º Passo: Se necessario criar um Job no Management Studio com o nome “DBA – Trace Querys Demoradas” e esse job deve possuir 3 steps.
Esse job insere esses registros na tabela Traces criada no passo 1.

7.1 Primeiro step: No primeiro Step devemos parar o trace momentaneamente para enviar todo o seu resultado para a tabela de log. Nesse step, você deve selecionar a database em que vc criou a tabela que armazenará o trace e incluir a query abaixo no step.

Declare @Trace_Id int
SELECT @Trace_Id = TraceId
FROM fn_trace_getinfo(0)
where value = 'E:\Trace\Querys_Demoradas\Querys_Demoradas.trc'
exec sp_trace_setstatus  @traceid = @Trace_Id,  @status = 0 -- Interrompe o rastreamento especificado.
exec sp_trace_setstatus  @traceid = @Trace_Id,  @status = 2 --Fecha o rastreamento especificado e exclui sua definição do servidor.
Insert Into Traces(Textdata, NTUserName, HostName, ApplicationName, LoginName, SPID, Duration, Starttime,
    EndTime, Reads,writes, CPU, Servername, DatabaseName, rowcounts, SessionLoginName)
Select Textdata, NTUserName, HostName, ApplicationName, LoginName, SPID, cast(Duration /1000/1000.00 as numeric(15,2)) Duration, Starttime,
    EndTime, Reads,writes, CPU, Servername, DatabaseName, rowcounts, SessionLoginName
FROM :: fn_trace_gettable('E:\Trace\Querys_Demoradas\Querys_Demoradas.trc', default)
where Duration is not null
order by Starttime

7.2 Segundo Step: Agora que os dados do trace já foram armazenados na tabela, deve-se excluir o arquivo de trace para que um novo seja criado. Isso pode ser realizado executando o comando del C:\Trace\Querys_Demoradas.trc /Q .
Para a criação desse step o mesmo deve ser criado com o tipo (Sistema Operacional(cmdexec))


7.3 terceiro Step: Esse passo deve apenas recriar o trace. Similar ao step 1, você deve selecionar a database em que vc criou a procedure e rodar a query abaixo:

exec dbo.stpCreate_Trace



http://www.fabriciolima.net/blog/2010/06/05/passo-a-passo-para-encontrar-as-querys-mais-demoradas-do-banco-de-dados-parte-1/



